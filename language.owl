#using owl.v4

.whitespace ' ' '\t' '\n'
.line-comment-token '//'

statements = statement*
statement = (
  constructor |
  method_call |
  routine_call |
  property_getter |
  assignment |
  routine_definition |
  rule_definition
) (';')?

actions = action*
action = (
  method_call |
  routine_call |
  assignment |
  await
) (';')?

constructor = module_name '=' module_type '(' ( argument (',' argument)* )? ')'
module_type = identifier

assignment = property_setter '=' expression

rule_definition = 'when' condition [ 'then' actions  ('else' actions)? 'end' ]

routine_definition = routine_name [ ':=' actions 'end' ]
routine_call = routine_name '(' ')'

await = 'await' condition

condition = expression comparison expression

expression = integer | neg_integer | number | neg_number | property_getter
comparison =
  '==' : equal
  '!=' : unequal

property_setter = module_name '.' property_name
property_getter = module_name '.' property_name
method_call = module_name '.' method '(' ( argument (',' argument)* )? ')'

argument = integer | neg_integer | number | neg_number | identifier | string
neg_integer = '-' integer
neg_number = '-' number
module_name = identifier
routine_name = identifier
property_name = identifier
method = identifier