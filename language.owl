#using owl.v4

.whitespace ' ' '\t' '\n'
.line-comment-token '//'

statements = statement{';'}
statement =
  expression |
  constructor |
  method_call |
  routine_call |
  property_assignment |
  variable_assignment |
  variable_declaration |
  routine_definition |
  rule_definition

actions = action{';'}
action =
  method_call |
  routine_call |
  property_assignment |
  variable_assignment |
  await

constructor = identifier@module_name '=' identifier@module_type [ '(' expression@argument{','} ')' ]
property_assignment = identifier@module_name '.' identifier@property_name '=' expression
variable_assignment = identifier@variable_name '=' expression
variable_declaration = datatype identifier@variable_name ('=' expression)?
rule_definition = 'when' expression@condition [ 'then' actions 'end' ]
routine_definition = identifier@routine_name [ ':=' actions 'end' ]
routine_call = identifier@routine_name [ '(' ')' ]
await = 'await' expression@condition
method_call = identifier@module_name '.' identifier@method_name [ '(' expression@argument{','} ')' ]

datatype =
    'bool' : boolean
    'int' : integer
    'float' : number
    'str' : string

expression =
    'true' : true
    'false' : false
    string : string
    integer : integer
    number : number
    identifier : variable
    identifier@module_name '.' identifier@property_name : property
    [ '(' expression ')' ] : parentheses
  .operators infix left
    '^' | '**' : power
  .operators prefix
    '-' : negate
  .operators infix left
    '*' : multiply
    '/' : divide
  .operators infix left
    '+' : add
    '-' : subtract
  .operators infix left
    '>' : greater
    '<' : less
    '>=' : greater_equal
    '<=' : less_equal
    '==' : equal
    '!=' : unequal
  .operators prefix
    '!' | 'not' : not
  .operators infix left
    '&&' | 'and' : and
    '||' | 'or' : or
